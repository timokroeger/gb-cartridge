; TODO: License

; The gameboy cartridge data and address signals are multiplexed with
; switchable transceivers.
;
; SET0-2 control the OE (output-enable) lines of the transceivers.
; SET3 selects the direction of the data transceiver (low=in, high=out).
;
; read_addr SM    |      SET      |                 IN                  |
; pin number    ->| 3 | 2 | 1 | 0 | 11  | 10  |  9  |  8  |    7..0     |
; ----------------+---+---+---+---+-----+-----+-----+-----+-------------+
; data_out_in SM->|                       IN/OUT                        |
; pin number    ->|15 |14 |13 |12 | 11  | 10  |  9  |  8  |    7..0     |
; ----------------+---+---+---+---+-----+-----+-----+-----+-------------+
;        DATA_IN  | 0 | 1 | 1 | 0 | CLK | #WR | #RD | #CS |  D7..D0 in  |
;        DATA_OUT | 1 | 1 | 1 | 0 | CLK | #WR | #RD | #CS |  D7..D0 out |
;        ADDR_LO  | 0 | 1 | 0 | 1 | CLK | #WR | #RD | #CS |  A7..A0     |
;        ADDR_HI  | 0 | 0 | 1 | 1 | CLK | #WR | #RD | #CS | A15..A8     |

.define public CARTRIDGE_BITS 12
.define public CONTROL_BITS 4

.define public CONTROL_MASK 0b1111
.define public DATA_IN      0b0110
.define public DATA_OUT     0b1110
.define public ADDR_LO      0b0101
.define public ADDR_HI      0b0011

.define public nCS 8
.define public nRD 9
.define public nWR 10
.define public CLK 11

; Bitmask for #RD (bit1) and #CS (bit0)
.define public ROM_READ  0b01
.define public MBC_WRITE 0b11
.define public RAM_READ  0b00
.define public RAM_WRITE 0b10

;------------------------------------------------------------------------------;

.program read_addr

    ; PICO clock = 125MHz
    ; GB clock = 4^22Hz = ~4Mhz
    ; BUS clock = GB clock / 4 = 2^10Hz = ~1MHz
    ;
    ; We use the rising edge of the BUS clock as reference (cycle 0) for PIO
    ; cycle counts within a BUS access cycle.
    ; The Gameboy writes the address to the bus at the first falling edge of
    ; the GB clock which means we need to wait 15 cyles for the address lines
    ; to become ready.
    ; Because PIO samples inputs very early the cycle we actually might have
    ; one cycle additional delay (just to keep in mind for later).
    ; Reference: https://forums.raspberrypi.com/viewtopic.php?t=321852#p1926757
    wait 0 pin CLK
    wait 1 pin CLK [14]     ; the delay starts after the pin is high   cycle  0

    ; Read the address but ignore A15, ADDR_HI is selected already.
    in pins, 7              ; A14-A8                                   cycle 15
    set pins, ADDR_LO ;                                                cycle 16
    in pins, 8              ; A7-A0, autopush                          cycle 17
    ; DMA flash access now runs in parallel

    irq 0                   ; notify address decoder to poll for data  cycle 18

    ; The Gameboy updates the A15 or and #CS lines on the second rising GB
    ; clock edge. Wait until cycle 30 to sample those.
    set pins, ADDR_HI [10]  ;                                          cycle 19
    in pins, 10             ; #RD, #CS, A15..A8                        cycle 30
    set pins, ADDR_LO       ;                                          cycle 31
    in pins, 11             ; A7-A0, autopush                          cycle 32

;------------------------------------------------------------------------------;

.program data_out_in

    ; Initialize X with the release data lines command.
    pull
    out x, 16

.wrap_target

    ; Data must be available at the third rising edge of GB clock which
    ; translates to a cycle count of 89.
    ; The loop of `read_addr` RX FIFO -> DMA chain -> `data_out_in` TX FIFO
    ; takes 62 cycles.

    ; Write output data (or not when selecting DATA_IN).
    pull                    ;                                 cycle 17 + 62 = 79
    out pins, 16            ;                                           cycle 80
    out pindirs, 16         ;                                           cycle 81
    ; Deadline is met with 8 cycles spare.
    ; TODO: Adjust ADDR_SAMPLE_DELAY to improve stablity.

    ; Read data. It will discarded later for DATA_OUT.
    in pins, 8

    ; Release the data lines at the end of the cycle.
    mov osr, x
    wait 1 pin CLK
    out pindirs, 16
    out pins, 16

.wrap
